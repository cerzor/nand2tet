// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    And(a=instruction[3], b=instruction[15], out=writeM);

    Mux16(a=instruction, b=aluout, sel=instruction[15], out=instMuxIn);

    Not(in=instruction[15], out=notoptcode);
    Or(a=instruction[5], b=notoptcode, out=ora);
    ARegister(in=instMuxIn, load=ora, out=ARegister, out[0..14]=addressM);
    
    And(a=instruction[12], b=instruction[15], out=dmuxload);
    Mux16(a=ARegister, b=inM, sel=dmuxload, out=AMMux);
    
    And(a=instruction[4], b=instruction[15], out=dand);
    DRegister(in=aluout, load=dand, out=DRegister);
    
    ALU(x=DRegister, y=AMMux, 
        zx=instruction[11], nx=instruction[10], 
        zy=instruction[9], ny=instruction[8], 
        f=instruction[7], no=instruction[6], out=aluout, out=outM, zr=zr, ng=ng);

        
    Not(in=zr, out=notzr);
    Not(in=ng, out=notng);
    
    And(a=notzr, b=notng, out=andnaluc);

    And(a=instruction[0], b=andnaluc, out=jmp1);
    And(a=instruction[1], b=zr, out=jmp2);
    And(a=instruction[2], b=ng, out=jmp3);

    Or(a=jmp1, b=jmp2, out=jmp12);
    Or(a=jmp12, b=jmp3, out=jmp123);
    And(a=instruction[15], b=jmp123, out=j);


    PC(in=ARegister, load=j, inc=true, reset=reset, out[0..14]=pc);

    

}